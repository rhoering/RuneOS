# $Id$
# Maintainer: Simone De Gregori <orion@runeaudio.com>

pkgbase=php
pkgname=php-rune
pkgdesc="PHP-FPM 5.5.19 (RuneAudio)"
packager="RuneAudio Team"
pkgver=5.5.19
pkgrel=1
pkgdir=${startdir}/pkg/${pkgname}
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('PHP')
url='http://www.php.net'
makedepends=('file' 'libmcrypt' 'libltdl' 'libpng' 'libjpeg' 'icu'
             'curl' 'bzip2' 'db' 'gmp' 'systemd')
source=("http://it2.php.net/distributions/${pkgbase}-${pkgver}.tar.gz"
        "http://it2.php.net/distributions/${pkgbase}-${pkgver}.tar.gz.asc"
        'logrotate.d.php-fpm' 'php-fpm.service' 'php-fpm.tmpfiles')

sha1sums=('SKIP'
	  'SKIP'
	  '688e35049f78011f514a87506890d56c5a15654d'
	  '09e9b0a73a921b063ac357faaf422af106b94487'
	  'a32b34b8f0262317c865260cf937fcbccbaf8927')

build() {
	cd ${srcdir}/php-${pkgver}
	local _phpconfig="--srcdir=../php-${pkgver} \
		--config-cache \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc/php \
		--localstatedir=/var \
		--with-layout=GNU \
		--with-config-file-path=/etc/php \
		--with-config-file-scan-dir=/etc/php/conf.d \
		--disable-rpath \
		--mandir=/usr/share/man \
		"

	local _phpextensions="--disable-all \
		--disable-cgi \
		--disable-ipv6 \
		--enable-static \
		--enable-fpm \
		--enable-bcmath=static \
		--enable-cli=static \
		--enable-ctype=static \
		--enable-fileinfo=static \
		--enable-mbstring=static \
		--enable-opcache=shared \
		--enable-phar=static \
		--enable-posix=static \
		--enable-shmop=static \
		--enable-sockets=static \
		--enable-sysvmsg=static \
		--enable-sysvsem=static \
		--enable-sysvshm=static \
		--enable-xml=static \
		--enable-simplexml=static \
		--enable-libxml=static \
		--enable-json=static \
		--enable-session=static \
		--with-bz2=static \
		--with-curl=static \
		--with-fpm-systemd \
		--with-gettext=static \
		--with-iconv \
		--with-mcrypt \
		--with-mhash \
		--with-openssl \
		--with-pcre-regex=/usr \
		--with-zlib \
		--with-readline \
		--with-pear \
		--with-fpm-user=http \
		--with-fpm-group=http \
	"

	EXTENSION_DIR=/usr/lib/php/modules
	export EXTENSION_DIR
	PEAR_INSTALLDIR=/usr/share/pear
	export PEAR_INSTALLDIR

	cd ${srcdir}/php-${pkgver}

	# php
	mkdir ${srcdir}/build-fpm
	cd ${srcdir}/build-fpm
	ln -s ../php-${pkgver}/configure
	./configure ${_phpconfig} ${_phpextensions}
	make
}

check() {
 	cd ${srcdir}/build-fpm
 	export SNMP_TIMEOUT=1
 	export SNMP_RETRIES=0
 	printf "n\n" | make test
 	echo
}

package() {
	pkgdesc='RuneAudio Optimized FastCGI Process Manager for PHP'
	depends=('pcre' 'libxml2' 'bzip2' 'curl' 'systemd')
	backup=('etc/php/php.ini')
	backup=('etc/php/php-fpm.conf')
	backup=('etc/php/pear.conf')
	install='php-fpm.install'

	cd ${srcdir}/build-fpm
	make -j1 INSTALL_ROOT=${pkgdir} install
	install -d -m755 ${pkgdir}/usr/share/pear
	# install RuneAudio php configuration
	rm -rf ${pkgdir}/etc/php
	cp -r ${startdir}/php-rune-config/ ${pkgdir}/etc/php
	chmod 755 ${pkgdir}/etc/php
	install -D -m755 ${srcdir}/build-fpm/sapi/fpm/php-fpm ${pkgdir}/usr/bin/php-fpm
	install -D -m644 ${srcdir}/build-fpm/sapi/fpm/php-fpm.8 ${pkgdir}/usr/share/man/man8/php-fpm.8
	install -D -m644 ${srcdir}/logrotate.d.php-fpm ${pkgdir}/etc/logrotate.d/php-fpm
	install -D -m644 ${srcdir}/php-fpm.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/php-fpm.conf
	install -D -m644 ${srcdir}/php-fpm.service ${pkgdir}/usr/lib/systemd/system/php-fpm.service
	make install-pear INSTALL_ROOT=${pkgdir}
	rm -rf ${pkgdir}/usr/share/pear/.{channels,depdb,depdblock,filemap,lock,registry}
	
	# remove static modules
	rm -f ${pkgdir}/usr/lib/php/modules/*.a
	# remove empty directory
	rmdir ${pkgdir}/usr/include/php/include
}

